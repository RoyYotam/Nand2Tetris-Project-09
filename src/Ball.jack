class Ball{
    field int x;
	field int y;
	field int textLine, textColumn;
	field int speed;
	
	constructor Ball new(){
        
		let x = 258;
		let y = 200;
		let textLine = 18;
		let textColumn = 31;
        let speed = -10;

        do drawBall();
        return this;
    }

	method void drawBall() {
        /*
            Called every time the ball is moving.
        */
        var String nbaString;

        // Draw black
        do Screen.setColor(true);
        
        // circle
        do Screen.drawCircle(x , y , 20);
		
		// Draw white
        do Screen.setColor(false);
        do Screen.drawCircle(x, y, 15);

        // Giving the ball a cool look.
        do Output.moveCursor(textLine, textColumn);

        // We will assign the 'NBA' as a string so we will be able to dispose the string.
        let nbaString = "NBA";
        do Output.printString(nbaString);
        do nbaString.dispose();

        return;
    }

	method int throwBall(int basketX) {
		/*
			return:
			0 -- missed
			1 -- scored
			2 -- still in play
		*/
		do clear();
		let y = y + speed;
		let textLine = textLine - 1;

		if (y < 80) {
			// set default values
			let y = 200;
			let textLine = 18;

			do drawBall();

			if ((x - basketX < 20) & (x - basketX > - 20)) {
				// H-I-T
				return 1;
			} else {
				// L-O-S-E-R
				return 0;
			}

		}

		do drawBall();
		return 2;

	}
    

	method void clear() {
		// Draw white
        do Screen.setColor(false);
        do Screen.drawCircle(x , y , 20);
		return;
	}
}